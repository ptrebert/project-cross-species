
ENSEMBL_REFERENCES_ASSEMBLIES = {
    'bonobo': 'fasta/pan_paniscus/dna/Pan_paniscus.panpan1.1.dna_sm.toplevel.fa.gz',
    'cat': 'fasta/felis_catus/dna/Felis_catus.Felis_catus_9.0.dna_sm.toplevel.fa.gz',
    'chicken': 'fasta/gallus_gallus/dna/Gallus_gallus.GRCg6a.dna_sm.toplevel.fa.gz',
    'chimpanzee': 'fasta/pan_troglodytes/dna/Pan_troglodytes.Pan_tro_3.0.dna_sm.toplevel.fa.gz',
    'cow': 'fasta/bos_taurus/dna/Bos_taurus.ARS-UCD1.2.dna_sm.toplevel.fa.gz',
    'dog': 'fasta/canis_familiaris/dna/Canis_familiaris.CanFam3.1.dna_sm.toplevel.fa.gz',
    'goat': 'fasta/capra_hircus/dna/Capra_hircus.ARS1.dna_sm.toplevel.fa.gz',
    'gorilla': 'fasta/gorilla_gorilla/dna/Gorilla_gorilla.gorGor4.dna_sm.toplevel.fa.gz',
    'horse': 'fasta/equus_caballus/dna/Equus_caballus.EquCab3.0.dna_sm.toplevel.fa.gz',
    'human': 'fasta/homo_sapiens/dna/Homo_sapiens.GRCh38.dna_sm.toplevel.fa.gz',
    'macaque': 'fasta/macaca_mulatta/dna/Macaca_mulatta.Mmul_8.0.1.dna_sm.toplevel.fa.gz',
    'mouse': 'fasta/mus_musculus/dna/Mus_musculus.GRCm38.dna_sm.toplevel.fa.gz',
    'opossum': 'fasta/monodelphis_domestica/dna/Monodelphis_domestica.monDom5.dna_sm.toplevel.fa.gz',
    'orangutan': 'fasta/pongo_abelii/dna/Pongo_abelii.PPYG2.dna_sm.toplevel.fa.gz',
    'pig': 'fasta/sus_scrofa/dna/Sus_scrofa.Sscrofa11.1.dna_sm.toplevel.fa.gz',
    'platypus': 'fasta/ornithorhynchus_anatinus/dna/Ornithorhynchus_anatinus.OANA5.dna_sm.toplevel.fa.gz',
    'rabbit': 'fasta/oryctolagus_cuniculus/dna/Oryctolagus_cuniculus.OryCun2.0.dna_sm.toplevel.fa.gz',
    'rat': 'fasta/rattus_norvegicus/dna/Rattus_norvegicus.Rnor_6.0.dna_sm.toplevel.fa.gz',
    'sheep': 'fasta/ovis_aries/dna/Ovis_aries.Oar_v3.1.dna_sm.toplevel.fa.gz'
}


ENSEMBL_REFERENCES_GENEMODELS = {
    'bonobo': 'gff3/pan_paniscus/Pan_paniscus.panpan1.1.96.gff3.gz',
    'cat': 'gff3/felis_catus/Felis_catus.Felis_catus_9.0.96.gff3.gz',
    'chicken': 'gff3/gallus_gallus/Gallus_gallus.GRCg6a.96.gff3.gz',
    'chimpanzee': 'gff3/pan_troglodytes/Pan_troglodytes.Pan_tro_3.0.96.gff3.gz',
    'cow': 'gff3/bos_taurus/Bos_taurus.ARS-UCD1.2.96.gff3.gz',
    'dog': 'gff3/canis_familiaris/Canis_familiaris.CanFam3.1.96.gff3.gz',
    'goat': 'gff3/capra_hircus/Capra_hircus.ARS1.96.gff3.gz',
    'gorilla': 'gff3/gorilla_gorilla/Gorilla_gorilla.gorGor4.96.gff3.gz',
    'horse': 'gff3/equus_caballus/Equus_caballus.EquCab3.0.96.gff3.gz',
    'human': 'gff3/homo_sapiens/Homo_sapiens.GRCh38.96.gff3.gz',
    'macaque': 'gff3/macaca_mulatta/Macaca_mulatta.Mmul_8.0.1.96.gff3.gz',
    'mouse': 'gff3/mus_musculus/Mus_musculus.GRCm38.96.gff3.gz',
    'opossum': 'gff3/monodelphis_domestica/Monodelphis_domestica.monDom5.96.gff3.gz',
    'orangutan': 'gff3/pongo_abelii/Pongo_abelii.PPYG2.96.gff3.gz',
    'pig': 'gff3/sus_scrofa/Sus_scrofa.Sscrofa11.1.96.gff3.gz',
    'platypus': 'gff3/ornithorhynchus_anatinus/Ornithorhynchus_anatinus.OANA5.96.gff3.gz',
    'rabbit': 'gff3/oryctolagus_cuniculus/Oryctolagus_cuniculus.OryCun2.0.96.gff3.gz',
    'rat': 'gff3/rattus_norvegicus/Rattus_norvegicus.Rnor_6.0.96.gff3.gz',
    'sheep': 'gff3/ovis_aries/Ovis_aries.Oar_v3.1.96.gff3.gz'
}


ENSEMBL_REFERENCES_TRANSCRIPT_SEQUENCES = {
    'bonobo': 'fasta/pan_paniscus/cdna/Pan_paniscus.panpan1.1.cdna.all.fa.gz',
    'cat': 'fasta/felis_catus/cdna/Felis_catus.Felis_catus_9.0.cdna.all.fa.gz',
    'chicken': 'fasta/gallus_gallus/cdna/Gallus_gallus.GRCg6a.cdna.all.fa.gz',
    'chimpanzee': 'fasta/pan_troglodytes/cdna/Pan_troglodytes.Pan_tro_3.0.cdna.all.fa.gz',
    'cow': 'fasta/bos_taurus/cdna/Bos_taurus.ARS-UCD1.2.cdna.all.fa.gz',
    'dog': 'fasta/canis_familiaris/cdna/Canis_familiaris.CanFam3.1.cdna.all.fa.gz',
    'goat': 'fasta/capra_hircus/cdna/Capra_hircus.ARS1.cdna.all.fa.gz',
    'gorilla': 'fasta/gorilla_gorilla/cdna/Gorilla_gorilla.gorGor4.cdna.all.fa.gz',
    'horse': 'fasta/equus_caballus/cdna/Equus_caballus.EquCab3.0.cdna.all.fa.gz',
    'human': 'fasta/homo_sapiens/cdna/Homo_sapiens.GRCh38.cdna.all.fa.gz',
    'macaque': 'fasta/macaca_mulatta/cdna/Macaca_mulatta.Mmul_8.0.1.cdna.all.fa.gz',
    'mouse': 'fasta/mus_musculus/cdna/Mus_musculus.GRCm38.cdna.all.fa.gz',
    'opossum': 'fasta/monodelphis_domestica/cdna/Monodelphis_domestica.monDom5.cdna.all.fa.gz',
    'orangutan': 'fasta/pongo_abelii/cdna/Pongo_abelii.PPYG2.cdna.all.fa.gz',
    'pig': 'fasta/sus_scrofa/cdna/Sus_scrofa.Sscrofa11.1.cdna.all.fa.gz',
    'platypus': 'fasta/ornithorhynchus_anatinus/cdna/Ornithorhynchus_anatinus.OANA5.cdna.all.fa.gz',
    'rabbit': 'fasta/oryctolagus_cuniculus/cdna/Oryctolagus_cuniculus.OryCun2.0.cdna.all.fa.gz',
    'rat': 'fasta/rattus_norvegicus/cdna/Rattus_norvegicus.Rnor_6.0.cdna.all.fa.gz',
    'sheep': 'fasta/ovis_aries/cdna/Ovis_aries.Oar_v3.1.cdna.all.fa.gz'
}


rule download_genome_assembly:
    input:
        'annotation/species/{species_name}.info'
    output:
        'references/assemblies/raw_download/{species_name}.fna.gz'
    wildcard_constraints:
        species_name = '[a-z]+'
    run:
        base_url = config['ensembl_release_url']
        load_path = ENSEMBL_REFERENCES_ASSEMBLIES[wildcards.species_name]
        source_url = os.path.join(base_url, load_path)
        exec = 'wget --quiet -O {output} ' + source_url
        shell(exec)


rule filter_sort_genome_assembly:
    input:
        'references/assemblies/raw_download/{species_name}.fna.gz'
    output:
        fasta = 'references/assemblies/whole-genome/{species_name}.wg.fa',
        sizes = 'references/chromosomes/whole-genome/{species_name}.wg.sizes',
        metrics = 'references/metrics/whole-genome/{species_name}.wg.metrics'
    log: 'log/references/filter_sort/whole-genome/{species_name}.log'
    wildcard_constraints:
        species_name = '[a-z]+'
    params:
        script_dir = config['script_dir'],
        log_config = config['script_log_config']
    run:
        exec = '{params.script_dir}/ensembl_references/process_ensembl_assembly.py'
        exec += ' --log-config {params.log_config}'
        exec += ' --fasta-in {input} --debug'
        exec += ' --fasta-out {output.fasta}'
        exec += ' --chromosome-sizes {output.sizes}'
        exec += ' --genome-metrics {output.metrics}'
        exec += ' --debug'
        exec += ' &> {log}'
        shell(exec)


rule build_bowtie_index:
    input:
        'references/assemblies/whole-genome/{species_name}.wg.fa'
    output:
        expand('references/indices/bowtie/{{species_name}}/{{species_name}}.{idxnum}.bt2',
                idxnum=[1, 2, 3, 4])
    log: 'log/references/indices/bowtie/{species_name}.log'
    benchmark: 'run/references/indices/bowtie/{species_name}.rsrc'
    wildcard_constraints:
        species_name = '[a-z]+'
    threads: 16
    run:
        exec = 'bowtie2-build --threads {threads}'
        exec += ' {input}'
        exec += ' references/indices/bowtie/{wildcards.species_name}/{wildcards.species_name}'
        exec += ' &> {log}'
        shell(exec)


rule download_gene_model:
    input:
        'annotation/species/{species_name}.info'
    output:
        'references/gene-models/raw_download/{species_name}.gff.gz'
    wildcard_constraints:
        species_name = '[a-z]+'
    run:
        base_url = config['ensembl_release_url']
        load_path = ENSEMBL_REFERENCES_GENEMODELS[wildcards.species_name]
        source_url = os.path.join(base_url, load_path)
        exec = 'wget --quiet -O {output} ' + source_url
        shell(exec)


rule process_gene_model:
    input:
        gff = 'references/gene-models/raw_download/{species_name}.gff.gz',
        tsv = 'references/chromosomes/whole-genome/{species_name}.wg.sizes'
    output:
        db = 'references/gene-models/whole-genome/{species_name}.sqlite',
        genes = 'references/gene-models/whole-genome/protein-coding/{species_name}.wg.genes.tsv',
        transcripts = 'references/gene-models/whole-genome/protein-coding/{species_name}.wg.transcripts.tsv',
        bodies = 'references/gene-models/whole-genome/protein-coding/{species_name}.wg.gene-body.bed',
        promoters = 'references/gene-models/whole-genome/protein-coding/{species_name}.wg.gene-promoter.bed',
        mapping = 'references/gene-models/whole-genome/protein-coding/{species_name}.wg.gt-map.tsv'
    log: 'log/references/gene-models/whole-genome/{species_name}.wg.protein-coding.log'
    wildcard_constraints:
        species_name = '[a-z]+'
    params:
        script_dir = config['script_dir'],
        log_config = config['script_log_config']
    run:
        exec = '{params.script_dir}/ensembl_references/process_ensembl_genemodel.py'
        exec += ' --log-config {params.log_config}'
        exec += ' --debug'
        if wildcards.species_name in ['human', 'mouse']:
            exec += ' --enforce-basic'
        exec += ' --gff-gene-model {input.gff}'
        exec += ' --db-gene-model {output.db}'
        exec += ' --chromosomes {input.tsv}'
        exec += ' --gene-transcript-map {output.mapping}'
        exec += ' --tsv-genes {output.genes}'
        exec += ' --tsv-transcripts {output.transcripts}'
        exec += ' --bed-gene-promoters {output.promoters}'
        exec += ' --bed-gene-bodies {output.bodies}'
        exec += ' &> {log}'
        shell(exec)


rule download_transcript_sequences:
    input:
        'annotation/species/{species_name}.info'
    output:
        'references/gene-models/raw_download/{species_name}.fa.gz'
    wildcard_constraints:
        species_name = '[a-z]+'
    run:
        base_url = config['ensembl_release_url']
        load_path = ENSEMBL_REFERENCES_TRANSCRIPT_SEQUENCES[wildcards.species_name]
        source_url = os.path.join(base_url, load_path)
        exec = 'wget --quiet -O {output} ' + source_url
        shell(exec)
        if wildcards.species_name == 'human':
            source_url = 'ftp://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_human/release_30/gencode.v30.pc_transcripts.fa.gz'
            output_dir = os.path.join(os.path.dirname(output[0]), 'validation')
            os.makedirs(output_dir, exist_ok=True)
            output_file = os.path.join(output_dir, 'human.gencode-v30.fa.gz')
            exec = 'wget --quiet -O ' + output_file + ' ' + source_url
            shell(exec)
        if wildcards.species_name == 'mouse':
            source_url = 'ftp://ftp.ebi.ac.uk/pub/databases/gencode/Gencode_mouse/release_M21/gencode.vM21.pc_transcripts.fa.gz'
            output_dir = os.path.join(os.path.dirname(output[0]), 'validation')
            os.makedirs(output_dir, exist_ok=True)
            output_file = os.path.join(output_dir, 'mouse.gencode-vM21.fa.gz')
            exec = 'wget --quiet -O ' + output_file + ' ' + source_url
            shell(exec)


rule process_transcript_sequences:
    input:
        fasta = 'references/gene-models/raw_download/{species_name}.fa.gz',
        table = 'references/gene-models/whole-genome/protein-coding/{species_name}.wg.transcripts.tsv'
    output:
        'references/gene-models/whole-genome/protein-coding/{species_name}.wg.transcripts.fa'
    log: 'log/references/gene-models/whole-genome/{species_name}.wg.transcript-seq.log'
    wildcard_constraints:
        species_name = '[a-z]+'
    params:
        script_dir = config['script_dir'],
        log_config = config['script_log_config']
    run:
        exec = '{params.script_dir}/ensembl_references/process_ensembl_transcript_sequences.py'
        exec += ' --log-config {params.log_config}'
        exec += ' --debug'
        if wildcards.species_name == 'human':
            check_file = os.path.join(os.path.dirname(input.fasta), 'validation', 'human.gencode-v30.fa.gz')
            exec += ' --fasta-compare ' + check_file
        if wildcards.species_name == 'mouse':
            check_file = os.path.join(os.path.dirname(input.fasta), 'validation', 'mouse.gencode-vM21.fa.gz')
            exec += ' --fasta-compare ' + check_file
        exec += ' --fasta-in {input.fasta}'
        exec += ' --transcripts {input.table}'
        exec += ' --fasta-out {output}'
        exec += ' &> {log}'
        shell(exec)